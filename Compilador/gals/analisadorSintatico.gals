#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
pp: "_" (i | f | b | s)
maiusculo: [A-Z]
minusculo: [a-z]
mindig: [a-z0-9]
inicio: ({maiusculo} {mindig} | {minusculo})
resto: ({maiusculo} {mindig} | {mindig})
int: 0 | [1-9][0-9]*
letra: (i | f | s | b)

#Tokens
palavra_reservada: [a-z]+

// identificador
identificador: {pp} ({maiusculo} | (({inicio}) ({resto})* {maiusculo}?))

// constante_int 
constante_int: {int}

// constante_float
constante_float: {int} "." [0-9](0* [1-9])*

// constante_string
constante_string: \" ([^\n\"\\"%"] | "%" {letra})* \"

// relaï¿½ï¿½o de palavras reservadas
// do if else false fun in main out repeat true while
// do = palavra: "do"
do = palavra_reservada: "do"
if = palavra_reservada: "if"
else = palavra_reservada: "else"
false = palavra_reservada: "false"
fun = palavra_reservada: "fun"
in = palavra_reservada: "in"
main = palavra_reservada: "main"
out = palavra_reservada: "out"
repeat = palavra_reservada: "repeat"
true = palavra_reservada: "true"
while= palavra_reservada: "while"

// relaï¿½ï¿½o de sï¿½mbolos especias
// & | ! , ; = : ( ) { } == != < > + - * /
"&"
"|"
"!"
","
";"
"="
":"
"("
")"
"{"
"}"
"=="
"!="
"<"
">"
"+"
"-"
"*"
"/" 

// reconher e ignorar caracteres de formataï¿½ï¿½o
// :[\n\t\s\r]
: [\n\t\s\r]

// reconhecer e ignorar comentï¿½rios de linha e de bloco
// :!{linha}
:! "#" (.)*
:! "[" ([^"[" "]"])* "]"
#NonTerminals
<geral>
<cmd>
<aux1>
<aux2>
<aux3>
<aux4>
<aux5>
<aux6>
<aux7>
<aux8>
<listinst>
<listid>
<listin>
<listexp>
<listcmd>
<instrucao>
<expressao>
<valor>
<entrada>
<saida>
<selecao>
<repeticao>
<elemento>
<expressao_>
<relacional>
<relacional_>
<operador_relacional>
<aritmetica>
<aritmetica_>
<termo>
<termo_>
<fator>
<atribuicao>
#Grammar
<geral> ::= #100 fun main "{" <listinst> "}" #101;


<listinst> ::= <instrucao> ";" <aux1>;
<aux1> ::= <listinst> | î;

<instrucao> ::= <listid> <aux8> | <entrada> | <saida> | <selecao> | <repeticao>;

<aux8> ::= ":" <valor> #126 | #127 î | "=" <expressao> #128;
<listid> ::= identificador #125 <aux2>;
<aux2> ::= "," <listid> | î;

<valor> ::= constante_int | constante_float | constante_string | false | true;

<cmd> ::= <entrada> | <saida> | <selecao> | <repeticao> | <atribuicao>;

<atribuicao> ::= <listid> "=" <expressao> #128;

<entrada> ::= in "(" <listin> ")";
<listin> ::= constante_string #130 "," <aux3> | <aux3>;
<aux3> ::= <listid> #139 <aux4>;
<aux4> ::= î | ";" <listin>;

<saida> ::= out "(" <listexp> ")";
<listexp> ::= <expressao> #102 <aux5>;
<aux5> ::= î | "," <listexp>;

<selecao> ::= if "(" <expressao> ")" #118 "{" <listcmd> "}" <aux6> #119;
<listcmd> ::= <cmd> ";" <aux7>;
<aux7> ::= î | <listcmd>;
<aux6> ::= î | #120 else "{" <listcmd> "}";

<repeticao> ::= #121 while "(" <expressao> ")" #122 do "{" <listcmd> "}" #123 | #121 repeat "{" <listcmd> "}" while "(" <expressao> ")" #124;



<expressao>			::= <elemento> <expressao_>  ;
<expressao_>			::= î | "&" <elemento> #103 <expressao_> | "|" <elemento> #104 <expressao_> ;
<elemento>			::= <relacional> | true #105 | false #106 | "!" <elemento> #107 ;

<relacional>			::= <aritmetica> <relacional_> ;
<relacional_>			::= î | <operador_relacional> #108 <aritmetica> #109;
<operador_relacional>	::= "==" | "!=" | "<" | ">" ;

<aritmetica>			::= <termo> <aritmetica_>  ;
<aritmetica_>			::= î | "+" <termo> #110 <aritmetica_> | "-" <termo> #111 <aritmetica_> ;

<termo>			::= <fator> <termo_> ;
<termo_>			::= î | "*" <fator> #112 <termo_> | "/" <fator> #113 <termo_> ;

<fator>			::= identificador #131 | 
			    	    constante_int #114 | 
		   	    	    constante_float #115 | 
			    	    constante_string #116 | 
		   	    	    "(" <expressao> ")" |
	    	  	    	    "+" <fator> | 
	    		    	    "-" <fator> #117;
